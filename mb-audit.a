;ACME 0.96.4

!cpu 6502	; Compatible with all Apple2's
!to "mb-audit", plain
!sl "mb-audit.labels"
*=$2000

;------------------------------------------------------------------------------

!source "AppleDefs.a"
!source "MockingboardDefs.a"

;------------------------------------------------------------------------------
; Mockingboard types supported:
;							$00						$80
; --------------------------------------------------------------------------
; Sound I					6522-A/AY8913			n/a
; Sound/Speech I			6522-A/SC01				6522-B/AY8913
; Sound II					6522-A/AY8913			6522-B/AY8913
; MB-C
; . MB-C & clones			6522-A/AY8913			6522-B/AY8913 +[1|2x SSI263]
; . MB-C (AppleWin)			MB-C +SC01				MB-C +SSI263
; . MEGA Audio (a2heaven)	MB-C					MB-C				6522 & AY8913 implemented in FPGA
; Phasor
; . MB-C					6522-A/AY8913			6522-B/AY8913 +[1|2x SSI263]
; . Echo+					6522-A/AY8913			6522-B/AY8913
; . Phasor native			6522-A/AY8913x2			6522-B/AY8913x2 +[1|2x SSI263]
; . Phasor (AppleWin)		Phasor +SC01			Phasor +SSI263
;
; Unsupported:
; Mockingboard 4c (Ian Kim)	6522-A/AY8913			6522-A/AY8913		Same 6522 mapped to both $00 & $80
;

;------------------------------------------------------------------------------

ZP_BASE = $f0

zpTmp3 = $fb
zpTmp2 = $fc
zpTmp1 = $fd
MBBase = $fe		; Mockingboard base
MBBaseL	= MBBase
MBBaseH	= MBBase+1

;------------------------------------------------------------------------------

!zone code

entrypoint
; Pre: TODO: disable accelerators or set to 1MHz
;

	sei

	ldx		#($ff-ZP_BASE)
-		lda		ZP_BASE,x
		sta		saveZP,x
		dex
		bpl		-

	lda		IRQL
	sta		saveIRQL
	lda		IRQH
	sta		saveIRQH

	;

	lda		TEXTON
	lda		MIXEDOFF
	lda		PAGE2OFF
	lda		HIRESOFF
	jsr		HOME	; perhaps better to clear to $00? ie. for floating-bus reads in GetSlotInfo()

	jsr		GetSlotInfo
	bne		+
		lda		#<msg_NoMB
		ldx		#>msg_NoMB
		jsr		Print
		jmp		exit

+	lda		#<msg_mbaudit
	ldx		#>msg_mbaudit
	jsr		Print
	jsr		Display

	lda		#7
	sta		slotUnderTest
@nextSlot
		ldx		slotUnderTest
		lda		slotInfo,x
		beq		+
			jsr		Diagnose6522
			bne		++
				jsr		PrintTestFailed
				jmp		exit
++			jsr		DetectSubunits	; AY8913 & speech chips
			ldx		slotUnderTest
			jsr		DisplayCardUpdate
			;
			lda		#1
			and		hasAY8913
			beq		++
				lda		#SY6522_A_BASE
				jsr		TestAY8913
				bne		++
					jsr		PrintTestFailedErrBytes
					jmp		exit
++			lda		#2
			and		hasAY8913
			beq		++
				lda		#SY6522_B_BASE
				jsr		TestAY8913
				bne		++
					jsr		PrintTestFailedErrBytes
					jmp		exit
++
			;
+		dec		slotUnderTest
		bne		@nextSlot

	lda		#<msg_OK
	ldx		#>msg_OK
	jsr		Print

	;

exit

	lda		saveIRQL
	sta		IRQL
	lda		saveIRQH
	sta		IRQH

	ldx		#($ff-ZP_BASE)
-		lda		saveZP,x
		sta		ZP_BASE,x
		dex
		bpl		-

	rts

;------------------------------------------------------------------------------

Diagnose6522
; Pre: X=slot#
; Post: Z=0(BNE):OK, Z=1(BEQ):Failed

	txa
	ora		#$c0
	sta		MBBaseH
	lda		#0
	sta		MBBaseL

	lda		#1
	sta		testNum
	lda		#0
	sta		subTestNum					; test #01.00

	lda		slotInfo,x
	and		#3							; just 6522's
	sta		zpTmp1

	inc		subTestNum					; test #01.01

	jsr		Diag6522Datalines
	bne		+
	rts

+	jsr		Diag6522IRQ
	rts

;------------------

Diag6522Datalines
; Pre: zpTmp1 = slotInfo[slot]
; Post: Z=0(BNE):OK, Z=1(BEQ):Failed

	;
	; Check r/w of all 8 data lines
	; (NB. my real Phasor occasionally powers-on with r/w to 2nd 6522's DDRA with b7=1!)
	;
	;                 1st loop  2nd loop
	; 6522-A:  DDRB =   $55       $AA
	; 6522-A:  DDRA =   $AA       $55
	; 6522-B:  DDRB =   $96       $69
	; 6522-B:  DDRA =   $69       $96
	; check A: DDRB ==  $55       $AA
	; check A: DDRA ==  $AA       $55
	; check B: DDRB ==  $69       $96
	; check B: DDRA ==  $96       $69

	lda		#0
	sta		zpTmp2

@loop
;@6522_A_w
		lda		zpTmp1
		and		#1
		beq		@6522_B_w

		ldy		#SY6522_A_BASE+SY6522_DDRB
		lda		#$55
		eor		zpTmp2
		sta		(MBBase),y
		lda		#$aa
		eor		zpTmp2
		iny								; Y=SY6522_DDRA
		sta		(MBBase),y

@6522_B_w
		lda		zpTmp1
		and		#2
		beq		@6522_A_r

		ldy		#SY6522_B_BASE+SY6522_DDRB
		lda		#$69
		eor		zpTmp2
		sta		(MBBase),y
		lda		#$96
		eor		zpTmp2
		iny								; Y=SY6522_DDRA
		sta		(MBBase),y

@6522_A_r
		lda		zpTmp1
		and		#1
		beq		@6522_B_r

		ldy		#SY6522_A_BASE+SY6522_DDRB
		lda		#$55
		eor		zpTmp2
		cmp		(MBBase),y
		bne		@6522_err
		lda		#$aa
		eor		zpTmp2
		iny								; Y=SY6522_DDRA
		cmp		(MBBase),y
		bne		@6522_err

@6522_B_r
		lda		zpTmp1
		and		#2
		beq		@next

		ldy		#SY6522_B_BASE+SY6522_DDRB
		lda		#$69
		eor		zpTmp2
		cmp		(MBBase),y
		bne		@6522_err
		lda		#$96
		eor		zpTmp2
		iny								; Y=SY6522_DDRA
		cmp		(MBBase),y
		bne		@6522_err

@next
	lda		#$ff
	eor		zpTmp2
	sta		zpTmp2
	bne		@loop

;

@6522_ok
	lda		#1
	rts

@6522_err
	lda		#0
	rts

;------------------

Diag6522IRQ
; Pre: zpTmp1 = slotInfo[slot]
; Post: Z=0(BNE):OK, Z=1(BEQ):Failed

	;
	; Check interrupt line
	;

	sei

	lda		#<Diag6522ISR
	sta		IRQL
	lda		#>Diag6522ISR
	sta		IRQH

;@6522_A_int
	lda		zpTmp1
	and		#1
	beq		@6522_B_int

	lda		#1
	ldy		#SY6522_A_BASE+SY6522_TIMER1L_COUNTER
	sta		(MBBase),y
	iny									; Y=SY6522_TIMER1H_COUNTER
	sta		(MBBase),y					; T1C=0x0101

	lda		#ACR_ONESHOT
	ldy		#SY6522_A_BASE+SY6522_ACR
	sta		(MBBase),y

	lda		#IxR_SET|IxR_TIMER1
	ldy		#SY6522_A_BASE+SY6522_IER
	sta		(MBBase),y

	lda		#$f							; wait 0x304 cycles
	jsr		WAIT

	ldy		#SY6522_A_BASE+SY6522_IFR
	lda		#IxR_TIMER1
	and		(MBBase),y
	beq		@6522_err

	cli
	sei

	and		isrIFR_A
	beq		@6522_err

@6522_B_int
	lda		zpTmp1
	and		#2
	beq		@6522_ok

	lda		#1
	ldy		#SY6522_B_BASE+SY6522_TIMER1L_COUNTER
	sta		(MBBase),y
	iny									; Y=SY6522_TIMER1H_COUNTER
	sta		(MBBase),y					; T1C=0x0101

	lda		#ACR_ONESHOT
	ldy		#SY6522_B_BASE+SY6522_ACR
	sta		(MBBase),y

	lda		#IxR_SET|IxR_TIMER1
	ldy		#SY6522_B_BASE+SY6522_IER
	sta		(MBBase),y

	lda		#$f							; wait 0x304 cycles
	jsr		WAIT

	ldy		#SY6522_B_BASE+SY6522_IFR
	lda		#IxR_TIMER1
	and		(MBBase),y
	beq		@6522_err

	cli
	sei

	and		isrIFR_B
	beq		@6522_err

;

@6522_ok
	lda		#1
	rts

@6522_err
	lda		#0
	rts

;------------------------------------------------------------------------------

Diag6522ISR
; Pre:
;	6502 has pushed P
;	Apple ROM has stored A to $45 (not Apple //e ROM!)
;

	txa
	pha
	tya
	pha

	;------

	ldy		#SY6522_A_BASE+SY6522_IFR
	lda		(MBBase),y
	sta		(MBBase),y
	sta		isrIFR_A

	ldy		#SY6522_B_BASE+SY6522_IFR
	lda		(MBBase),y
	sta		(MBBase),y
	sta		isrIFR_B

	;------

	pla
	tay
	pla
	tax

	lda		$45		; for Apple II/II+
	rti


;------------------------------------------------------------------------------

DetectSubunits
; Detect Phasor card, AY8913, SSI263 & SC01
; Pre: zpTmp1 = slotInfo[slot]

	lda		#0
	sta		isPhasorCard			; assume Mockingboard
	sta		hasSSI263				; ... no SSI263's
	sta		hasSC01					; ... no SC01
	sta		hasAY8913				; ... no AY81913's

	; Determine if this is a Mockingboard or Phasor card

	lda		zpTmp1
	and		#1
	beq		@detectSSI263

	lda		#$55
	ldy		#SY6522_DDRB
	sta		(MBBase),y
	asl								; A=$AA
	iny								; SY6522_DDRA
	sta		(MBBase),y

	lda		#PH_PHASOR
	jsr		SetPhasorMode

	; Phasor card in Phasor mode has floating-bus at $Cn00-$Cn0F
	ldy		#SY6522_DDRB
	lda		#$55
	cmp		(MBBase),y
	bne		+
	asl								; A=$AA
	iny								; SY6522_DDRA
	cmp		(MBBase),y
	bne		+
	ldy		#SY6522_TIMER1L_COUNTER
	jsr		SF_GetTimerL
	beq		++
+		inc		isPhasorCard
++	lda		#PH_MOCKINGBOARD
	jsr		SetPhasorMode

	;

@detectSSI263
	; SSI263 is only on MB-C & Phasor, both of which have 2x 6522's
	; - Speech using 6522 PCR w/o IRQ (based on Willy Byte's SSI263 detection code)

	lda		zpTmp1
	and		#1
	beq		@detectSC01

	lda		#IxR_SSI263
	ldy		#SY6522_B_BASE+SY6522_IER
	sta		(MBBase),y				; disable CA1(Speech) IRQ
	ldy		#SY6522_B_BASE+SY6522_IFR
	sta		(MBBase),y				; clear CA1(Speech) IRQ

	lda		#$0C					; Peripheral for PA port Control: CA2 = low output (Pin18: Power Down/Reset)
									; CA1 = negative active edge (input) (Pin4: A/!R)
	ldy		#SY6522_B_BASE+SY6522_PCR
	sta		(MBBase),y

	lda		#CONTROL_H
	ldy		#SSI_CTTRAMP
	sta		(MBBase),y

	; Warning: stepping this won't yield an IRQ, since the phoneme completes before H->L (assuming prev mode=MODE_IRQ_DISABLED)
	lda		#MODE_PHONEME_TRANSITIONED_INFLECTION+$00	; DR1:DR0=3 , P5:P0=$00
	ldy		#SSI_DURPHON
	sta		(MBBase),y

	lda		#$70					; H->L sets SSI263 operation mode = DR1:DR0, ie. IRQ active (also set Articulation=7, as Willy Byte does)
	ldy		#SSI_CTTRAMP
	sta		(MBBase),y

	lda		#$FF
	jsr		WAIT					; delay for 0x28882 (166018) cycles / ~163ms

	ldy		#SY6522_B_BASE+SY6522_IFR
	lda		#IxR_SSI263				; check CA1(Speech) IRQ
	and		(MBBase),y
	beq		+
	sta		(MBBase),y				; clear CA1(Speech) IRQ

		jsr		DisableSSI263
		inc		hasSSI263

+

	;

@detectSC01
	; SC01 is only on MB-Speech, MB-Sound/Speech + AppleWin's hybrid MB
	; - Speech using 6522 PCR w/o IRQ (based on Crimewave's SC01 code)

	lda		#IxR_VOTRAX
	ldy		#SY6522_A_BASE+SY6522_IER
	sta		(MBBase),y				; disable CB1(Speech) IRQ
	ldy		#SY6522_A_BASE+SY6522_IFR
	sta		(MBBase),y				; clear CB1(Speech) IRQ

	ldy		#SY6522_A_BASE+SY6522_DDRB
	lda		#$FF
	sta		(MBBase),y

	lda		#$B0
	ldy		#SY6522_A_BASE+SY6522_PCR
	sta		(MBBase),y

	ldy		#SY6522_A_BASE+SY6522_ORB
	lda		#$03					; PAUSE0
	sta		(MBBase),y

	lda		#$FF
	jsr		WAIT					; delay for 0x28882 (166018) cycles / ~163ms

	ldy		#SY6522_A_BASE+SY6522_IFR
	lda		#IxR_VOTRAX				; check CB1(Speech) IRQ
	and		(MBBase),y
	beq		+
	sta		(MBBase),y				; clear CB1(Speech) IRQ

		jsr		DisableSC01
		inc		hasSC01

+

	;

@detectAY8913
	lda		#SY6522_A_BASE
	sta		zpTmp3
	lda		#1					; hasAY8913 mask
	sta		zpTmp2

-		lsr		zpTmp1			; 6522 at this addr?
		bcc		+

		lda		#SY6522_DDRB
		ora		zpTmp3
		tay
		lda		#$07
		sta		(MBBase),y

		lda		#SY6522_DDRA
		ora		zpTmp3
		tay
		lda		#$ff	; output
		sta		(MBBase),y

		ldy		zpTmp3
		jsr		SF_ChipReset	; Post: Y=SY6522_ORA for chip A or B

		;

		lda		#AY_AFINE
		ldx		#$AA
		jsr		AY_WriteRegValue

		lda		#AY_BFINE
		ldx		#$55
		jsr		AY_WriteRegValue

		lda		#AY_CFINE
		ldx		#$69
		jsr		AY_WriteRegValue

		;

		ldx		#AY_AFINE
		jsr		AY_ReadRegValue
		cmp		#$AA
		bne		+

		ldx		#AY_BFINE
		jsr		AY_ReadRegValue
		cmp		#$55
		bne		+

		ldx		#AY_CFINE
		jsr		AY_ReadRegValue
		cmp		#$69
		bne		+

			lda		hasAY8913
			ora		zpTmp2
			sta		hasAY8913

+		asl		zpTmp2			; mask <<= 1
		lda		zpTmp3
		eor		#$80
		sta		zpTmp3
		bne		-

	;

+	lda		#1
	rts

;------------------------------------------------------------------------------

TestAY8913
; Pre: A = SY6522_A_BASE or SY6522_B_BASE

	sta		zpTmp3

	lda		#SY6522_DDRB
	ora		zpTmp3
	tay
	lda		#$07
	sta		(MBBase),y

	lda		#SY6522_DDRA
	ora		zpTmp3
	tay
	lda		#$ff	; output
	sta		(MBBase),y

	ldy		zpTmp3
	jsr		SF_ChipReset		; Post: Y=SY6522_ORA for chip A or B

	; Test the 8 data lines:
	; . Write then Read: $01, $02, $04, ..., $80

	lda		#2
	sta		testNum
	lda		#0
	sta		subTestNum			; test #02.00

	lda		#1					; data to test
	sta		zpTmp2

-		lda		#AY_AFINE
		ldx		zpTmp2
		jsr		AY_WriteRegValue

		ldx		#AY_AFINE
		jsr		AY_ReadRegValue
		cmp		zpTmp2
		bne		@error

		jsr		AY_SetPortAOutput	; DDRA=0xFF

		asl		zpTmp2
		bne		-

	; Test the 4 addr lines:
	; . Write regs: [0..$D] with values [0..$D]
	; . then read back

	inc		subTestNum			; test #02.01

	lda		#0					; reg & data to test
	sta		zpTmp2

-		lda		zpTmp2
		tax
		jsr		AY_WriteRegValue

--		inc		zpTmp2
		lda		zpTmp2
		cmp		#AY_ENABLE		; skip ENABLE to avoid enabling ABC noise
		beq		--
		cmp		#AY_ESHAPE
		bne		-

	lda		#0					; reg & data to test
	sta		zpTmp2

-		ldx		zpTmp2
		jsr		AY_ReadRegValue
		cmp		zpTmp2
		bne		@error

--		inc		zpTmp2
		lda		zpTmp2
		cmp		#AY_ENABLE		; skip ENABLE to avoid enabling ABC noise
		beq		--
		cmp		#AY_ESHAPE
		bne		-

	ldy		zpTmp3
	jsr		SF_ChipReset		; Post: Y=SY6522_ORA for chip A or B
	jsr		AY_SetPortAOutput	; DDRA=0xFF (restore this)

	lda		#1
	rts

@error
; Pre: zpTmp2 = expected value
;	A = actual value

	sta		errorActual
	lda		zpTmp2
	sta		errorExpected

	lda		#0
	rts

;------------------------------------------------------------------------------

SetPhasorMode
; Pre: A=phasorMode

	sta		phasorMode
	lda		MBBaseH
	asl
	asl
	asl
	asl								; slot * 16
	clc
	adc		phasorMode
	tax
	lda		$c080,x
	rts

;--------------------------------------

DisableSSI263

	lda		#CONTROL_H
	ldy		#SSI_CTTRAMP
	sta		(MBBase),y

	lda		#MODE_IRQ_DISABLED+$00	; DR1:DR0=3 , P5:P0=$00
	ldy		#SSI_DURPHON
	sta		(MBBase),y

	lda		#CONTROL_L				; H->L sets SSI263 operation mode = DR1:DR0, ie. IRQ inactive
	ldy		#SSI_CTTRAMP
	sta		(MBBase),y

	rts

;--------------------------------------

DisableSC01

	; https://github.com/a2-4am/4cade:
	; . turn off cb2 pulse mode to disable false writes to sc01 (to fix random MB noise)
	lda		#$D0
	ldy		#SY6522_A_BASE+SY6522_PCR
	sta		(MBBase),y

	rts

;------------------------------------------------------------------------------

Display
;              1234567
;          $00:  1 CP		;V=6522(VIA), 1=Sound-I, S=Sound/Speech, C=MB-C(or Sound-II), P=Phasor
;          $80:   VCP
;           SP:    S2		;V(ortax)=SC01, S(SI263)=SSI263, H(ybrid)=V+S, 2=S+S, 3=V+S+S

	lda		#$80+'$'
	sta		$480+$1d
	sta		$500+$1d
	lda		#$80+'0'
	sta		$480+$1e
	sta		$480+$1f
	sta		$500+$1f
	lda		#$80+'8'
	sta		$500+$1e
	lda		#$80+':'
	sta		$480+$20
	sta		$500+$20
	sta		$580+$20
	lda		#$80+'S'
	sta		$580+$1e
	lda		#$80+'P'
	sta		$580+$1f

	ldx		#7

-		txa
		ora		#$B0
		sta		$400+$20,x		; '1234567'

		lda		slotInfo,x
		lsr
		pha
		lda		#$80+' '		; C-flag not affected
		bcc		+
		lda		#$80+'V'
+		sta		$480+$20,x		; 'V' or ' '

		pla
		lsr
		lda		#$80+' '		; C-flag not affected
		bcc		+
		lda		#$80+'V'
+		sta		$500+$20,x		; 'V' or ' '

		dex
		bne		-

	lda		#$80+$0a
	jsr		COUT
	lda		#$80+$0a
	jsr		COUT
	lda		#$80+$0a
	jsr		COUT

	rts

;--------------------------------------

DisplayCardUpdate
; Pre: X=slot#

	;
	; Update cards
	;

+	lda		hasAY8913
	cmp		#1					; 6522/AY8913 at $00?
	bne		+
		lda		#$80+'1'		; MB-Sound I (has a 6522/AY8913 at $00)
		sta		$480+$20,x

+	lda		hasAY8913
	cmp		#2					; 6522/AY8913 at $80?
	bne		+
		lda		hasSC01
		beq		+				; no SC01 - don't know what this is!
		lda		#$80+'S'		; MB-Sound/Speech I has a 6522/SC01 at $00; and a 6522/AY8913 at $80
		sta		$500+$20,x

+	lda		hasAY8913
	cmp		#3					; 6522/AY8913 at $00 && $80?
	bne		+
		lda		isPhasorCard
		beq		@isIIorC
			lda		#$80+'P'
			bne		@out
@isIIorC	lda		#$80+'C'	; MB-C or MB-Sound II (has 2x 6522/AY8913, but no SC01 nor SSI263)
@out:	sta		$480+$20,x
		sta		$500+$20,x

	;
+	; Add info about any speech chips
	;

	lda		hasSC01
	beq		@noSC01
		lda		hasSSI263
		bne		++
			lda		#$80+'V'	; SC01
			bne		@outSP
++		cmp		#1
		bne		+++
			lda		#$80+'H'	; SC01 && 1x SSI263
			bne		@outSP
+++		lda		#$80+'3'		; SC01 && 2x SSI263
@outSP	sta		$580+$20,x
	jmp		@done

@noSC01
	lda		hasSSI263
	beq		@done

	cmp		#1
	bne		+
		lda		#$80+'S'		; 1x SSI263
		bne		++
+		lda		#$80+'2'		; 2x SSI263
++		sta		$580+$20,x

@done
	rts

;------------------------------------------------------------------------------

GetSlotInfo
; Scan all slots for 6522s at $Cn00 and $Cn80
; Post: Z=1 (BEQ) if nothing found

	ldx		#7
-		lda		#0
		sta		slotInfo,x
		jsr		Detect
		dex
		bne		-

	lda		#0
	ldx		#7
-		ora		slotInfo,x
		dex
		bne		-
	tax
	rts

;

Detect
; Pre: X=slot#
; Post: X=slot#

	txa
	ora		#$c0
	sta		MBBaseH
	lda		#0
	sta		MBBaseL

	; 6522's IER.b7 is always 1
	; . Due to floating bus (and the visible TEXT screen being mainly $A0 values), then empty slots will likely pass this IER.b7 check
	ldy		#SY6522_B_BASE+SY6522_IER
	lda		(MBBase),y
	bmi		@check_A
	rts

@check_A
	ldy		#SY6522_A_BASE+SY6522_TIMER1L_COUNTER
	jsr		Check6522
	bne		+
		inc		slotInfo,x	; bit0=1
+

@check_B
	ldy		#SY6522_B_BASE+SY6522_TIMER1L_COUNTER
	jsr		Check6522
	bne		+
		inc		slotInfo,x
		inc		slotInfo,x	; bit1=1
+

	rts

Check6522
; Pre: Y = SY6522_TIMER1L_COUNTER for chip A or B
; Post: Z=1(OK), Z=0(NG)
	lda		#4				; try 4 checks (avoid floating bus giving a false-positive, and avoid T1C_L underflow giving a false-negative!)
	sta		zpTmp1
	lda		#1				; failures (can have 1 failure)
	sta		zpTmp2
-		jsr		SF_GetTimerL
		beq		+
			dec		zpTmp2
			bmi		++
+		dec		zpTmp1
		bne		-
++	rts

SF_GetTimerL				; From Skyfox's detection code
; Pre: Y = SY6522_TIMER1L_COUNTER for chip A or B
; Post: Z=1(OK), Z=0(NG)
	lda		(MBBase),y
	cmp		MBBaseL			; 3cy, C=1 since A>=0 as (MBBaseL) == 0x00
	sbc		(MBBase),y		; 5cy
	cmp		#$08
	beq		+
	cmp		#$09			; FASTChip //e (Ref: https://github.com/a2-4am/4cade)
+	rts

;------------------------------------------------------------------------------

AY_WriteRegValue
; Pre: Y = SY6522_ORA for chip A or B
;	A=reg#
;	X=value
	jsr		SF_SelectReg
	txa
	jmp		SF_WriteReg

AY_ReadRegValue
; Pre: Y = SY6522_ORA for chip A or B
;	X=reg#
; Post:
;	A=value
	jsr		AY_SetPortAOutput
	txa
	jsr		SF_SelectReg
	jsr		AY_SetPortAInput
	jmp		AY_ReadReg

	;

AY_SetPortAOutput
; Pre: Y = SY6522_ORA for chip A or B
; Post: Y = SY6522_ORA for chip A or B
	iny
	iny		; Y = DDRA
	lda		#$ff
	sta		(MBBase),y
	dey
	dey		; Y = ORA
	rts

AY_SetPortAInput
; Pre: Y = SY6522_ORA for chip A or B
; Post: Y = SY6522_ORA for chip A or B
	iny
	iny		; Y = DDRA
	lda		#$0
	sta		(MBBase),y
	dey
	dey		; Y = ORA
	rts

	;

AY_ReadReg
; Pre: Y = SY6522_ORA for chip A or B
;	6522.DDRA = $00 (Port-A direction = input)
; Post: Y = SY6522_ORA for chip A or B
;	A = data
	lda		#AY_READ
	jsr		+
	lda		(MBBase),y		; Read Port-A
	rts

SF_SelectReg
; Pre: Y = SY6522_ORA for chip A or B
; Post: Y = SY6522_ORA for chip A or B
;	A = reg#
	sta		(MBBase),y
	lda		#AY_LATCH
	bne		+

SF_WriteReg
; Pre: Y = SY6522_ORA for chip A or B
;	6522.DDRA = $FF (Port-A direction = output)
; Post: Y = SY6522_ORA for chip A or B
;	A = data
	sta		(MBBase),y
	lda		#AY_WRITE
	bne		+

SF_ChipReset
; Pre: Y = SY6522_A_BASE or SY6522_B_BASE
; Post: Y = SY6522_ORA for chip A or B
	lda		#AY_RESET
	beq		++

+	dey		; Y = ORB (reg#0)
++	sta		(MBBase),y
	lda		#AY_INACTIVE
	sta		(MBBase),y
	iny		; Y = ORA (reg#1)
	rts

;------------------------------------------------------------------------------

PrintTestFailedErrBytes
	jsr		PrintTestFailed
	lda		#<msg_Expected
	ldx		#>msg_Expected
	jsr		Print
	ldx		errorExpected
	jsr		PRNTX
	lda		#<msg_Actual
	ldx		#>msg_Actual
	jsr		Print
	ldx		errorActual
	jsr		PRNTX
	lda		#$80+$0d
	jmp		COUT

PrintTestFailed
	lda		#<msg_TestFailed
	ldx		#>msg_TestFailed
	jsr		Print
	ldx		testNum
	jsr		PRNTX
	lda		#$80+':'
	jsr		COUT
	ldx		subTestNum
	jsr		PRNTX
	lda		#$80+$0d
	jmp		COUT

;------------------------------------------------------------------------------

Print
; Pre: A(l),X(h) = msg addr

	sta		$fe
	stx		$ff
	ldy		#0
@loop	lda		($fe),y
		beq		+
		ora		#$80	; set high bit
		tax
		tya
		pha
		txa
		jsr		COUT
		pla
		tay
		iny
		bne 	@loop
+	rts

;------------------------------------------------------------------------------

!zone data

!align 15, 0, 0		; align to 16 bytes (pad with 0's)

saveZP			!fill ($100-ZP_BASE),0
slotInfo		!fill 8,0	; slot[0..7] : bits: 0=6522/$00, 1=6522/$80

slotUnderTest	!byte 0

testNum			!byte 0
subTestNum		!byte 0

isPhasorCard	!byte 0
phasorMode:		!byte PH_MOCKINGBOARD

hasAY8913		!byte 0
hasSSI263		!byte 0
hasSC01			!byte 0

isrIFR_A		!byte 0
isrIFR_B		!byte 0

saveIRQL		!byte 0
saveIRQH		!byte 0

errorExpected	!byte 0
errorActual		!byte 0

msg_mbaudit	!text "mb-audit v0.01, 2021",0xd
msg_mbauditEnd !text 0

msg_NoMB !text "No Mockingboard detected!",0xd
msg_NoMBEnd !text 0

msg_TestFailed !text "Mockingboard failed test #"	; 00:00 (testNum:subTestNum)
msg_TestFailedEnd !text 0

msg_Expected !text "Expected:"	; 00 (errorExpected)
msg_ExpectedEnd !text 0

msg_Actual !text " Actual:"	; 00 (errorActual)
msg_ActualEnd !text 0

msg_OK !text "All tests passed",0xd
msg_OKEnd !text 0
